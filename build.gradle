buildscript {
    repositories {
        jcenter()
        maven {url "http://developer.marklogic.com/maven2/"}
    }
    dependencies {
        classpath("org.springframework.boot:spring-boot-gradle-plugin:1.2.5.RELEASE")
    }
}

apply plugin: 'java'
apply plugin: 'spring-boot'

repositories {
    jcenter()
}

configurations {
    // MarkLogic Java API contains Logback, which breaks spring-boot's
    // logging.   Excluding it allows logging to work as intended
    compile.exclude module: 'spring-boot-starter-logging'
}

dependencies {
    compile ("com.marklogic:java-client-api:3.0.3")
    compile("org.springframework.boot:spring-boot-starter-web")
    compile("org.springframework.boot:spring-boot-starter-actuator")
}

// install the JS libraries used by this application
task bower(type: Exec) {
    commandLine "bower", 'install'
}

// create and setup the application.properties file based on settings in the
// gradle.properties (allows one base config file)
task springConfigure() {
  ant.propertyfile(
    file: "src/main/resources/application.properties") {
      entry( key: "server.port", value: port)
      entry( key: "logging.level.org.springframework.web", value: hibernateLog)
      entry( key: "logging.level.org.hibernate", value: springLog)

    }
}

// run the app, and run all the dependency tasks first
task runApp(dependsOn: ['bower', 'bootRun', 'springConfigure']) {
  springConfigure.mustRunAfter bower
  bootRun.mustRunAfter springConfigure
}
