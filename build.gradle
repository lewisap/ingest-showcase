buildscript {
    repositories {
        jcenter()
        mavenCentral()
        maven {url "http://developer.marklogic.com/maven2/"}
        maven {url "http://rjrudin.github.io/marklogic-java/releases"}
    }
    dependencies {
        classpath("org.springframework.boot:spring-boot-gradle-plugin:1.2.5.RELEASE")
        classpath "com.marklogic:ml-gradle:2.0-alpha-4"
        classpath 'org.codehaus.groovy.modules.http-builder:http-builder:0.7'
    }
}

apply plugin: 'java'
apply plugin: 'spring-boot'
apply plugin: 'ml-gradle'
apply plugin: 'maven-publish'

ext {
  mlAppConfig {
    modulePaths = ["ml-server/src/main/xqy"]

    configDir.contentDatabaseFiles.add(new File("ml-server/src/main/ml-config/databases/obi-ids-content-database.json"))
    configDir.contentDatabaseFiles.add(new File("ml-server/src/test/ml-config/databases/test-content-database.json"))
  }
}

repositories {
    jcenter()
    mavenCentral()
    maven {url "http://developer.marklogic.com/maven2/"}
    maven {url "http://rjrudin.github.io/marklogic-java/releases"}
}

configurations {
    // MarkLogic Java API contains Logback, which breaks spring-boot's
    // logging.   Excluding it allows logging to work as intended
    compile.exclude module: 'spring-boot-starter-logging'
    xquery
}

dependencies {
    compile ("com.marklogic:java-client-api:3.0.3")
    compile("org.springframework.boot:spring-boot-starter-web")
    compile("org.springframework.boot:spring-boot-starter-actuator")
    compile 'com.marklogic:ml-javaclient-util:2.0.3'
    compile 'com.fasterxml.jackson.core:jackson-databind:2.5.2'
    compile 'com.fasterxml.jackson.core:jackson-core:2.5.2'
    compile 'org.apache.commons:commons-lang3:3.4'
}

// install the JS libraries used by this application
task bower(type: Exec) {
    commandLine "bower", 'install'
}

// create and setup the application.properties file based on settings in the
// gradle.properties (allows one base config file)
task springConfigure() {
  ant.propertyfile(
    file: "src/main/resources/application.properties") {
      entry( key: "server.port", value: port)
      entry( key: "logging.level.org.springframework.web", value: hibernateLog)
      entry( key: "logging.level.org.hibernate", value: springLog)

    }
}

// run the app, and run all the dependency tasks first
task runApp(dependsOn: ['bower', 'bootRun', 'springConfigure']) {
  springConfigure.mustRunAfter bower
  bootRun.mustRunAfter springConfigure
}
